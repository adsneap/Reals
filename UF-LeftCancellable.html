<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>UF-LeftCancellable</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Background">Left cancellable maps.

The definition is given in UF-Base. Here we prove things about them.

</a><a id="95" class="Markup">\begin{code}</a>

<a id="109" class="Symbol">{-#</a> <a id="113" class="Keyword">OPTIONS</a> <a id="121" class="Pragma">--without-K</a> <a id="133" class="Pragma">--exact-split</a> <a id="147" class="Pragma">--safe</a> <a id="154" class="Pragma">--auto-inline</a> <a id="168" class="Symbol">#-}</a>

<a id="173" class="Keyword">module</a> <a id="180" href="UF-LeftCancellable.html" class="Module">UF-LeftCancellable</a> <a id="199" class="Keyword">where</a>

<a id="206" class="Keyword">open</a> <a id="211" class="Keyword">import</a> <a id="218" href="SpartanMLTT.html" class="Module">SpartanMLTT</a>
<a id="230" class="Keyword">open</a> <a id="235" class="Keyword">import</a> <a id="242" href="UF-Base.html" class="Module">UF-Base</a>
<a id="250" class="Keyword">open</a> <a id="255" class="Keyword">import</a> <a id="262" href="UF-Subsingletons.html" class="Module">UF-Subsingletons</a>
<a id="279" class="Keyword">open</a> <a id="284" class="Keyword">import</a> <a id="291" href="UF-Retracts.html" class="Module">UF-Retracts</a>
<a id="303" class="Keyword">open</a> <a id="308" class="Keyword">import</a> <a id="315" href="UF-Equiv.html" class="Module">UF-Equiv</a>

<a id="left-cancellable-reflects-is-prop"></a><a id="325" href="UF-LeftCancellable.html#325" class="Function">left-cancellable-reflects-is-prop</a> <a id="359" class="Symbol">:</a> <a id="361" class="Symbol">{</a><a id="362" href="UF-LeftCancellable.html#362" class="Bound">X</a> <a id="364" class="Symbol">:</a> <a id="366" href="Universes.html#274" class="Generalizable">𝓤</a> <a id="368" href="Universes.html#417" class="Function Operator">̇</a> <a id="370" class="Symbol">}</a> <a id="372" class="Symbol">{</a><a id="373" href="UF-LeftCancellable.html#373" class="Bound">Y</a> <a id="375" class="Symbol">:</a> <a id="377" href="Universes.html#276" class="Generalizable">𝓥</a> <a id="379" href="Universes.html#417" class="Function Operator">̇</a> <a id="381" class="Symbol">}</a> <a id="383" class="Symbol">(</a><a id="384" href="UF-LeftCancellable.html#384" class="Bound">f</a> <a id="386" class="Symbol">:</a> <a id="388" href="UF-LeftCancellable.html#362" class="Bound">X</a> <a id="390" class="Symbol">→</a> <a id="392" href="UF-LeftCancellable.html#373" class="Bound">Y</a><a id="393" class="Symbol">)</a>
                                 <a id="428" class="Symbol">→</a> <a id="430" href="GeneralNotation.html#1154" class="Function">left-cancellable</a> <a id="447" href="UF-LeftCancellable.html#384" class="Bound">f</a> <a id="449" class="Symbol">→</a> <a id="451" href="UF-Subsingletons.html#487" class="Function">is-prop</a> <a id="459" href="UF-LeftCancellable.html#373" class="Bound">Y</a> <a id="461" class="Symbol">→</a> <a id="463" href="UF-Subsingletons.html#487" class="Function">is-prop</a> <a id="471" href="UF-LeftCancellable.html#362" class="Bound">X</a>
<a id="473" href="UF-LeftCancellable.html#325" class="Function">left-cancellable-reflects-is-prop</a> <a id="507" href="UF-LeftCancellable.html#507" class="Bound">f</a> <a id="509" href="UF-LeftCancellable.html#509" class="Bound">lc</a> <a id="512" href="UF-LeftCancellable.html#512" class="Bound">i</a> <a id="514" href="UF-LeftCancellable.html#514" class="Bound">x</a> <a id="516" href="UF-LeftCancellable.html#516" class="Bound">x&#39;</a> <a id="519" class="Symbol">=</a> <a id="521" href="UF-LeftCancellable.html#509" class="Bound">lc</a> <a id="524" class="Symbol">(</a><a id="525" href="UF-LeftCancellable.html#512" class="Bound">i</a> <a id="527" class="Symbol">(</a><a id="528" href="UF-LeftCancellable.html#507" class="Bound">f</a> <a id="530" href="UF-LeftCancellable.html#514" class="Bound">x</a><a id="531" class="Symbol">)</a> <a id="533" class="Symbol">(</a><a id="534" href="UF-LeftCancellable.html#507" class="Bound">f</a> <a id="536" href="UF-LeftCancellable.html#516" class="Bound">x&#39;</a><a id="538" class="Symbol">))</a>

<a id="section-lc"></a><a id="542" href="UF-LeftCancellable.html#542" class="Function">section-lc</a> <a id="553" class="Symbol">:</a> <a id="555" class="Symbol">{</a><a id="556" href="UF-LeftCancellable.html#556" class="Bound">X</a> <a id="558" class="Symbol">:</a> <a id="560" href="Universes.html#274" class="Generalizable">𝓤</a> <a id="562" href="Universes.html#417" class="Function Operator">̇</a> <a id="564" class="Symbol">}</a> <a id="566" class="Symbol">{</a><a id="567" href="UF-LeftCancellable.html#567" class="Bound">A</a> <a id="569" class="Symbol">:</a> <a id="571" href="Universes.html#276" class="Generalizable">𝓥</a> <a id="573" href="Universes.html#417" class="Function Operator">̇</a> <a id="575" class="Symbol">}</a> <a id="577" class="Symbol">(</a><a id="578" href="UF-LeftCancellable.html#578" class="Bound">s</a> <a id="580" class="Symbol">:</a> <a id="582" href="UF-LeftCancellable.html#556" class="Bound">X</a> <a id="584" class="Symbol">→</a> <a id="586" href="UF-LeftCancellable.html#567" class="Bound">A</a><a id="587" class="Symbol">)</a> <a id="589" class="Symbol">→</a> <a id="591" href="UF-Retracts.html#322" class="Function">is-section</a> <a id="602" href="UF-LeftCancellable.html#578" class="Bound">s</a> <a id="604" class="Symbol">→</a> <a id="606" href="GeneralNotation.html#1154" class="Function">left-cancellable</a> <a id="623" href="UF-LeftCancellable.html#578" class="Bound">s</a>
<a id="625" href="UF-LeftCancellable.html#542" class="Function">section-lc</a> <a id="636" class="Symbol">{</a><a id="637" href="UF-LeftCancellable.html#637" class="Bound">𝓤</a><a id="638" class="Symbol">}</a> <a id="640" class="Symbol">{</a><a id="641" href="UF-LeftCancellable.html#641" class="Bound">𝓥</a><a id="642" class="Symbol">}</a> <a id="644" class="Symbol">{</a><a id="645" href="UF-LeftCancellable.html#645" class="Bound">X</a><a id="646" class="Symbol">}</a> <a id="648" class="Symbol">{</a><a id="649" href="UF-LeftCancellable.html#649" class="Bound">Y</a><a id="650" class="Symbol">}</a> <a id="652" href="UF-LeftCancellable.html#652" class="Bound">s</a> <a id="654" class="Symbol">(</a><a id="655" href="UF-LeftCancellable.html#655" class="Bound">r</a> <a id="657" href="Sigma-Type.html#202" class="InductiveConstructor Operator">,</a> <a id="659" href="UF-LeftCancellable.html#659" class="Bound">rs</a><a id="661" class="Symbol">)</a> <a id="663" class="Symbol">{</a><a id="664" href="UF-LeftCancellable.html#664" class="Bound">x</a><a id="665" class="Symbol">}</a> <a id="667" class="Symbol">{</a><a id="668" href="UF-LeftCancellable.html#668" class="Bound">y</a><a id="669" class="Symbol">}</a> <a id="671" href="UF-LeftCancellable.html#671" class="Bound">p</a> <a id="673" class="Symbol">=</a> <a id="675" class="Symbol">(</a><a id="676" href="UF-LeftCancellable.html#659" class="Bound">rs</a> <a id="679" href="UF-LeftCancellable.html#664" class="Bound">x</a><a id="680" class="Symbol">)</a><a id="681" href="Id.html#1263" class="Function Operator">⁻¹</a> <a id="684" href="Id.html#1176" class="Function Operator">∙</a> <a id="686" href="Id.html#1344" class="Function">ap</a> <a id="689" href="UF-LeftCancellable.html#655" class="Bound">r</a> <a id="691" href="UF-LeftCancellable.html#671" class="Bound">p</a> <a id="693" href="Id.html#1176" class="Function Operator">∙</a> <a id="695" href="UF-LeftCancellable.html#659" class="Bound">rs</a> <a id="698" href="UF-LeftCancellable.html#668" class="Bound">y</a>

<a id="is-equiv-lc"></a><a id="701" href="UF-LeftCancellable.html#701" class="Function">is-equiv-lc</a> <a id="713" class="Symbol">:</a> <a id="715" class="Symbol">{</a><a id="716" href="UF-LeftCancellable.html#716" class="Bound">X</a> <a id="718" class="Symbol">:</a> <a id="720" href="Universes.html#274" class="Generalizable">𝓤</a> <a id="722" href="Universes.html#417" class="Function Operator">̇</a> <a id="724" class="Symbol">}</a> <a id="726" class="Symbol">{</a><a id="727" href="UF-LeftCancellable.html#727" class="Bound">Y</a> <a id="729" class="Symbol">:</a> <a id="731" href="Universes.html#276" class="Generalizable">𝓥</a> <a id="733" href="Universes.html#417" class="Function Operator">̇</a> <a id="735" class="Symbol">}</a> <a id="737" class="Symbol">(</a><a id="738" href="UF-LeftCancellable.html#738" class="Bound">f</a> <a id="740" class="Symbol">:</a> <a id="742" href="UF-LeftCancellable.html#716" class="Bound">X</a> <a id="744" class="Symbol">→</a> <a id="746" href="UF-LeftCancellable.html#727" class="Bound">Y</a><a id="747" class="Symbol">)</a> <a id="749" class="Symbol">→</a> <a id="751" href="UF-Equiv.html#359" class="Function">is-equiv</a> <a id="760" href="UF-LeftCancellable.html#738" class="Bound">f</a> <a id="762" class="Symbol">→</a> <a id="764" href="GeneralNotation.html#1154" class="Function">left-cancellable</a> <a id="781" href="UF-LeftCancellable.html#738" class="Bound">f</a>
<a id="783" href="UF-LeftCancellable.html#701" class="Function">is-equiv-lc</a> <a id="795" href="UF-LeftCancellable.html#795" class="Bound">f</a> <a id="797" class="Symbol">(_</a> <a id="800" href="Sigma-Type.html#202" class="InductiveConstructor Operator">,</a> <a id="802" href="UF-LeftCancellable.html#802" class="Bound">hasr</a><a id="806" class="Symbol">)</a> <a id="808" class="Symbol">=</a> <a id="810" href="UF-LeftCancellable.html#542" class="Function">section-lc</a> <a id="821" href="UF-LeftCancellable.html#795" class="Bound">f</a> <a id="823" href="UF-LeftCancellable.html#802" class="Bound">hasr</a>

<a id="left-cancellable-closed-under-∘"></a><a id="829" href="UF-LeftCancellable.html#829" class="Function">left-cancellable-closed-under-∘</a> <a id="861" class="Symbol">:</a> <a id="863" class="Symbol">{</a><a id="864" href="UF-LeftCancellable.html#864" class="Bound">X</a> <a id="866" class="Symbol">:</a> <a id="868" href="Universes.html#274" class="Generalizable">𝓤</a> <a id="870" href="Universes.html#417" class="Function Operator">̇</a> <a id="872" class="Symbol">}</a> <a id="874" class="Symbol">{</a><a id="875" href="UF-LeftCancellable.html#875" class="Bound">Y</a> <a id="877" class="Symbol">:</a> <a id="879" href="Universes.html#276" class="Generalizable">𝓥</a> <a id="881" href="Universes.html#417" class="Function Operator">̇</a> <a id="883" class="Symbol">}</a> <a id="885" class="Symbol">{</a><a id="886" href="UF-LeftCancellable.html#886" class="Bound">Z</a> <a id="888" class="Symbol">:</a> <a id="890" href="Universes.html#278" class="Generalizable">𝓦</a> <a id="892" href="Universes.html#417" class="Function Operator">̇</a> <a id="894" class="Symbol">}</a> <a id="896" class="Symbol">(</a><a id="897" href="UF-LeftCancellable.html#897" class="Bound">f</a> <a id="899" class="Symbol">:</a> <a id="901" href="UF-LeftCancellable.html#864" class="Bound">X</a> <a id="903" class="Symbol">→</a> <a id="905" href="UF-LeftCancellable.html#875" class="Bound">Y</a><a id="906" class="Symbol">)</a> <a id="908" class="Symbol">(</a><a id="909" href="UF-LeftCancellable.html#909" class="Bound">g</a> <a id="911" class="Symbol">:</a> <a id="913" href="UF-LeftCancellable.html#875" class="Bound">Y</a> <a id="915" class="Symbol">→</a> <a id="917" href="UF-LeftCancellable.html#886" class="Bound">Z</a><a id="918" class="Symbol">)</a>
                                <a id="952" class="Symbol">→</a> <a id="954" href="GeneralNotation.html#1154" class="Function">left-cancellable</a> <a id="971" href="UF-LeftCancellable.html#897" class="Bound">f</a> <a id="973" class="Symbol">→</a> <a id="975" href="GeneralNotation.html#1154" class="Function">left-cancellable</a> <a id="992" href="UF-LeftCancellable.html#909" class="Bound">g</a> <a id="994" class="Symbol">→</a> <a id="996" href="GeneralNotation.html#1154" class="Function">left-cancellable</a> <a id="1013" class="Symbol">(</a><a id="1014" href="UF-LeftCancellable.html#909" class="Bound">g</a> <a id="1016" href="Pi.html#531" class="Function Operator">∘</a> <a id="1018" href="UF-LeftCancellable.html#897" class="Bound">f</a><a id="1019" class="Symbol">)</a>
<a id="1021" href="UF-LeftCancellable.html#829" class="Function">left-cancellable-closed-under-∘</a> <a id="1053" href="UF-LeftCancellable.html#1053" class="Bound">f</a> <a id="1055" href="UF-LeftCancellable.html#1055" class="Bound">g</a> <a id="1057" href="UF-LeftCancellable.html#1057" class="Bound">lcf</a> <a id="1061" href="UF-LeftCancellable.html#1061" class="Bound">lcg</a> <a id="1065" class="Symbol">=</a> <a id="1067" href="UF-LeftCancellable.html#1057" class="Bound">lcf</a> <a id="1071" href="Pi.html#531" class="Function Operator">∘</a> <a id="1073" href="UF-LeftCancellable.html#1061" class="Bound">lcg</a>

<a id="NatΣ-lc"></a><a id="1078" href="UF-LeftCancellable.html#1078" class="Function">NatΣ-lc</a> <a id="1086" class="Symbol">:</a> <a id="1088" class="Symbol">{</a><a id="1089" href="UF-LeftCancellable.html#1089" class="Bound">X</a> <a id="1091" class="Symbol">:</a> <a id="1093" href="Universes.html#274" class="Generalizable">𝓤</a> <a id="1095" href="Universes.html#417" class="Function Operator">̇</a> <a id="1097" class="Symbol">}</a> <a id="1099" class="Symbol">{</a><a id="1100" href="UF-LeftCancellable.html#1100" class="Bound">A</a> <a id="1102" class="Symbol">:</a> <a id="1104" href="UF-LeftCancellable.html#1089" class="Bound">X</a> <a id="1106" class="Symbol">→</a> <a id="1108" href="Universes.html#276" class="Generalizable">𝓥</a> <a id="1110" href="Universes.html#417" class="Function Operator">̇</a> <a id="1112" class="Symbol">}</a> <a id="1114" class="Symbol">{</a><a id="1115" href="UF-LeftCancellable.html#1115" class="Bound">B</a> <a id="1117" class="Symbol">:</a> <a id="1119" href="UF-LeftCancellable.html#1089" class="Bound">X</a> <a id="1121" class="Symbol">→</a> <a id="1123" href="Universes.html#278" class="Generalizable">𝓦</a> <a id="1125" href="Universes.html#417" class="Function Operator">̇</a> <a id="1127" class="Symbol">}</a> <a id="1129" class="Symbol">(</a><a id="1130" href="UF-LeftCancellable.html#1130" class="Bound">f</a> <a id="1132" class="Symbol">:</a> <a id="1134" href="UF-Base.html#172" class="Function">Nat</a> <a id="1138" href="UF-LeftCancellable.html#1100" class="Bound">A</a> <a id="1140" href="UF-LeftCancellable.html#1115" class="Bound">B</a><a id="1141" class="Symbol">)</a>
        <a id="1151" class="Symbol">→</a> <a id="1153" class="Symbol">((</a><a id="1155" href="UF-LeftCancellable.html#1155" class="Bound">x</a> <a id="1157" class="Symbol">:</a> <a id="1159" href="UF-LeftCancellable.html#1089" class="Bound">X</a><a id="1160" class="Symbol">)</a> <a id="1162" class="Symbol">→</a> <a id="1164" href="GeneralNotation.html#1154" class="Function">left-cancellable</a><a id="1180" class="Symbol">(</a><a id="1181" href="UF-LeftCancellable.html#1130" class="Bound">f</a> <a id="1183" href="UF-LeftCancellable.html#1155" class="Bound">x</a><a id="1184" class="Symbol">))</a>
        <a id="1195" class="Symbol">→</a> <a id="1197" href="GeneralNotation.html#1154" class="Function">left-cancellable</a> <a id="1214" class="Symbol">(</a><a id="1215" href="UF-Base.html#690" class="Function">NatΣ</a> <a id="1220" href="UF-LeftCancellable.html#1130" class="Bound">f</a><a id="1221" class="Symbol">)</a>
<a id="1223" href="UF-LeftCancellable.html#1078" class="Function">NatΣ-lc</a> <a id="1231" class="Symbol">{</a><a id="1232" href="UF-LeftCancellable.html#1232" class="Bound">𝓤</a><a id="1233" class="Symbol">}</a> <a id="1235" class="Symbol">{</a><a id="1236" href="UF-LeftCancellable.html#1236" class="Bound">𝓥</a><a id="1237" class="Symbol">}</a> <a id="1239" class="Symbol">{</a><a id="1240" href="UF-LeftCancellable.html#1240" class="Bound">𝓦</a><a id="1241" class="Symbol">}</a> <a id="1243" class="Symbol">{</a><a id="1244" href="UF-LeftCancellable.html#1244" class="Bound">X</a><a id="1245" class="Symbol">}</a> <a id="1247" class="Symbol">{</a><a id="1248" href="UF-LeftCancellable.html#1248" class="Bound">A</a><a id="1249" class="Symbol">}</a> <a id="1251" class="Symbol">{</a><a id="1252" href="UF-LeftCancellable.html#1252" class="Bound">B</a><a id="1253" class="Symbol">}</a> <a id="1255" href="UF-LeftCancellable.html#1255" class="Bound">f</a> <a id="1257" href="UF-LeftCancellable.html#1257" class="Bound">flc</a> <a id="1261" class="Symbol">{</a><a id="1262" href="UF-LeftCancellable.html#1262" class="Bound">x</a> <a id="1264" href="Sigma-Type.html#202" class="InductiveConstructor Operator">,</a> <a id="1266" href="UF-LeftCancellable.html#1266" class="Bound">a</a><a id="1267" class="Symbol">}</a> <a id="1269" class="Symbol">{</a><a id="1270" href="UF-LeftCancellable.html#1270" class="Bound">x&#39;</a> <a id="1273" href="Sigma-Type.html#202" class="InductiveConstructor Operator">,</a> <a id="1275" href="UF-LeftCancellable.html#1275" class="Bound">a&#39;</a><a id="1277" class="Symbol">}</a> <a id="1279" href="UF-LeftCancellable.html#1279" class="Bound">p</a> <a id="1281" class="Symbol">=</a> <a id="1283" href="UF-Base.html#11025" class="Function">to-Σ-≡</a> <a id="1290" class="Symbol">(</a><a id="1291" href="Id.html#1344" class="Function">ap</a> <a id="1294" href="Sigma-Type.html#217" class="Field">pr₁</a> <a id="1298" href="UF-LeftCancellable.html#1279" class="Bound">p</a> <a id="1300" href="Sigma-Type.html#202" class="InductiveConstructor Operator">,</a> <a id="1302" href="UF-LeftCancellable.html#1314" class="Function">γ</a><a id="1303" class="Symbol">)</a>
 <a id="1306" class="Keyword">where</a>
  <a id="1314" href="UF-LeftCancellable.html#1314" class="Function">γ</a> <a id="1316" class="Symbol">:</a> <a id="1318" href="Id.html#1075" class="Function">transport</a> <a id="1328" href="UF-LeftCancellable.html#1248" class="Bound">A</a> <a id="1330" class="Symbol">(</a><a id="1331" href="Id.html#1344" class="Function">ap</a> <a id="1334" href="Sigma-Type.html#217" class="Field">pr₁</a> <a id="1338" href="UF-LeftCancellable.html#1279" class="Bound">p</a><a id="1339" class="Symbol">)</a> <a id="1341" href="UF-LeftCancellable.html#1266" class="Bound">a</a> <a id="1343" href="Identity-Type.html#135" class="Datatype Operator">≡</a> <a id="1345" href="UF-LeftCancellable.html#1275" class="Bound">a&#39;</a>
  <a id="1350" href="UF-LeftCancellable.html#1314" class="Function">γ</a> <a id="1352" class="Symbol">=</a> <a id="1354" href="UF-LeftCancellable.html#1257" class="Bound">flc</a> <a id="1358" href="UF-LeftCancellable.html#1270" class="Bound">x&#39;</a> <a id="1361" class="Symbol">(</a><a id="1362" href="UF-LeftCancellable.html#1255" class="Bound">f</a> <a id="1364" href="UF-LeftCancellable.html#1270" class="Bound">x&#39;</a> <a id="1367" class="Symbol">(</a><a id="1368" href="Id.html#1075" class="Function">transport</a> <a id="1378" href="UF-LeftCancellable.html#1248" class="Bound">A</a> <a id="1380" class="Symbol">(</a><a id="1381" href="Id.html#1344" class="Function">ap</a> <a id="1384" href="Sigma-Type.html#217" class="Field">pr₁</a> <a id="1388" href="UF-LeftCancellable.html#1279" class="Bound">p</a><a id="1389" class="Symbol">)</a> <a id="1391" href="UF-LeftCancellable.html#1266" class="Bound">a</a><a id="1392" class="Symbol">)</a> <a id="1394" href="Id.html#1757" class="Function Operator">≡⟨</a> <a id="1397" href="UF-Base.html#4750" class="Function">nat-transport</a> <a id="1411" href="UF-LeftCancellable.html#1255" class="Bound">f</a> <a id="1413" class="Symbol">(</a><a id="1414" href="Id.html#1344" class="Function">ap</a> <a id="1417" href="Sigma-Type.html#217" class="Field">pr₁</a> <a id="1421" href="UF-LeftCancellable.html#1279" class="Bound">p</a><a id="1422" class="Symbol">)</a> <a id="1424" href="Id.html#1757" class="Function Operator">⟩</a>
              <a id="1440" href="Id.html#1075" class="Function">transport</a> <a id="1450" href="UF-LeftCancellable.html#1252" class="Bound">B</a> <a id="1452" class="Symbol">(</a><a id="1453" href="Id.html#1344" class="Function">ap</a> <a id="1456" href="Sigma-Type.html#217" class="Field">pr₁</a> <a id="1460" href="UF-LeftCancellable.html#1279" class="Bound">p</a><a id="1461" class="Symbol">)</a> <a id="1463" class="Symbol">(</a><a id="1464" href="UF-LeftCancellable.html#1255" class="Bound">f</a> <a id="1466" href="UF-LeftCancellable.html#1262" class="Bound">x</a> <a id="1468" href="UF-LeftCancellable.html#1266" class="Bound">a</a><a id="1469" class="Symbol">)</a>  <a id="1472" href="Id.html#1757" class="Function Operator">≡⟨</a> <a id="1475" href="UF-Base.html#10591" class="Function">from-Σ-≡&#39;</a> <a id="1485" href="UF-LeftCancellable.html#1279" class="Bound">p</a> <a id="1487" href="Id.html#1757" class="Function Operator">⟩</a>
              <a id="1503" href="UF-LeftCancellable.html#1255" class="Bound">f</a> <a id="1505" href="UF-LeftCancellable.html#1270" class="Bound">x&#39;</a> <a id="1508" href="UF-LeftCancellable.html#1275" class="Bound">a&#39;</a>                         <a id="1535" href="Id.html#1839" class="Function Operator">∎</a><a id="1536" class="Symbol">)</a>

<a id="NatΠ-lc"></a><a id="1539" href="UF-LeftCancellable.html#1539" class="Function">NatΠ-lc</a> <a id="1547" class="Symbol">:</a> <a id="1549" class="Symbol">{</a><a id="1550" href="UF-LeftCancellable.html#1550" class="Bound">X</a> <a id="1552" class="Symbol">:</a> <a id="1554" href="Universes.html#274" class="Generalizable">𝓤</a> <a id="1556" href="Universes.html#417" class="Function Operator">̇</a> <a id="1558" class="Symbol">}</a> <a id="1560" class="Symbol">{</a><a id="1561" href="UF-LeftCancellable.html#1561" class="Bound">A</a> <a id="1563" class="Symbol">:</a> <a id="1565" href="UF-LeftCancellable.html#1550" class="Bound">X</a> <a id="1567" class="Symbol">→</a> <a id="1569" href="Universes.html#276" class="Generalizable">𝓥</a> <a id="1571" href="Universes.html#417" class="Function Operator">̇</a> <a id="1573" class="Symbol">}</a> <a id="1575" class="Symbol">{</a><a id="1576" href="UF-LeftCancellable.html#1576" class="Bound">B</a> <a id="1578" class="Symbol">:</a> <a id="1580" href="UF-LeftCancellable.html#1550" class="Bound">X</a> <a id="1582" class="Symbol">→</a> <a id="1584" href="Universes.html#278" class="Generalizable">𝓦</a> <a id="1586" href="Universes.html#417" class="Function Operator">̇</a> <a id="1588" class="Symbol">}</a> <a id="1590" class="Symbol">(</a><a id="1591" href="UF-LeftCancellable.html#1591" class="Bound">f</a> <a id="1593" class="Symbol">:</a> <a id="1595" href="UF-Base.html#172" class="Function">Nat</a> <a id="1599" href="UF-LeftCancellable.html#1561" class="Bound">A</a> <a id="1601" href="UF-LeftCancellable.html#1576" class="Bound">B</a><a id="1602" class="Symbol">)</a>
        <a id="1612" class="Symbol">→</a> <a id="1614" class="Symbol">((</a><a id="1616" href="UF-LeftCancellable.html#1616" class="Bound">x</a> <a id="1618" class="Symbol">:</a> <a id="1620" href="UF-LeftCancellable.html#1550" class="Bound">X</a><a id="1621" class="Symbol">)</a> <a id="1623" class="Symbol">→</a> <a id="1625" href="GeneralNotation.html#1154" class="Function">left-cancellable</a><a id="1641" class="Symbol">(</a><a id="1642" href="UF-LeftCancellable.html#1591" class="Bound">f</a> <a id="1644" href="UF-LeftCancellable.html#1616" class="Bound">x</a><a id="1645" class="Symbol">))</a>
        <a id="1656" class="Symbol">→</a> <a id="1658" class="Symbol">{</a><a id="1659" href="UF-LeftCancellable.html#1659" class="Bound">g</a> <a id="1661" href="UF-LeftCancellable.html#1661" class="Bound">g&#39;</a> <a id="1664" class="Symbol">:</a> <a id="1666" href="Pi.html#182" class="Function">Π</a> <a id="1668" href="UF-LeftCancellable.html#1561" class="Bound">A</a><a id="1669" class="Symbol">}</a> <a id="1671" class="Symbol">→</a> <a id="1673" href="UF-Base.html#790" class="Function">NatΠ</a> <a id="1678" href="UF-LeftCancellable.html#1591" class="Bound">f</a> <a id="1680" href="UF-LeftCancellable.html#1659" class="Bound">g</a> <a id="1682" href="Identity-Type.html#135" class="Datatype Operator">≡</a> <a id="1684" href="UF-Base.html#790" class="Function">NatΠ</a> <a id="1689" href="UF-LeftCancellable.html#1591" class="Bound">f</a> <a id="1691" href="UF-LeftCancellable.html#1661" class="Bound">g&#39;</a> <a id="1694" class="Symbol">→</a> <a id="1696" href="UF-LeftCancellable.html#1659" class="Bound">g</a> <a id="1698" href="Id.html#1581" class="Function Operator">∼</a> <a id="1700" href="UF-LeftCancellable.html#1661" class="Bound">g&#39;</a>
<a id="1703" href="UF-LeftCancellable.html#1539" class="Function">NatΠ-lc</a> <a id="1711" href="UF-LeftCancellable.html#1711" class="Bound">f</a> <a id="1713" href="UF-LeftCancellable.html#1713" class="Bound">flc</a> <a id="1717" class="Symbol">{</a><a id="1718" href="UF-LeftCancellable.html#1718" class="Bound">g</a><a id="1719" class="Symbol">}</a> <a id="1721" class="Symbol">{</a><a id="1722" href="UF-LeftCancellable.html#1722" class="Bound">g&#39;</a><a id="1724" class="Symbol">}</a> <a id="1726" href="UF-LeftCancellable.html#1726" class="Bound">p</a> <a id="1728" href="UF-LeftCancellable.html#1728" class="Bound">x</a> <a id="1730" class="Symbol">=</a> <a id="1732" href="UF-LeftCancellable.html#1713" class="Bound">flc</a> <a id="1736" href="UF-LeftCancellable.html#1728" class="Bound">x</a> <a id="1738" class="Symbol">(</a><a id="1739" href="UF-Base.html#7493" class="Function">happly</a> <a id="1746" href="UF-LeftCancellable.html#1726" class="Bound">p</a> <a id="1748" href="UF-LeftCancellable.html#1728" class="Bound">x</a><a id="1749" class="Symbol">)</a>

<a id="1752" class="Markup">\end{code}</a><a id="1762" class="Background">

Sometimes the type of left cancellable maps is useful (but more often
the type of embeddings, defined elsewhere, is useful).

</a><a id="1890" class="Markup">\begin{code}</a>

<a id="_↣_"></a><a id="1904" href="UF-LeftCancellable.html#1904" class="Function Operator">_↣_</a> <a id="1908" class="Symbol">:</a> <a id="1910" href="Universes.html#274" class="Generalizable">𝓤</a> <a id="1912" href="Universes.html#417" class="Function Operator">̇</a> <a id="1914" class="Symbol">→</a> <a id="1916" href="Universes.html#276" class="Generalizable">𝓥</a> <a id="1918" href="Universes.html#417" class="Function Operator">̇</a> <a id="1920" class="Symbol">→</a> <a id="1922" href="Universes.html#274" class="Generalizable">𝓤</a> <a id="1924" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="1926" href="Universes.html#276" class="Generalizable">𝓥</a> <a id="1928" href="Universes.html#417" class="Function Operator">̇</a>
<a id="1930" href="UF-LeftCancellable.html#1930" class="Bound">X</a> <a id="1932" href="UF-LeftCancellable.html#1904" class="Function Operator">↣</a> <a id="1934" href="UF-LeftCancellable.html#1934" class="Bound">Y</a> <a id="1936" class="Symbol">=</a>  <a id="1939" href="Sigma.html#436" class="Function">Σ</a> <a id="1941" href="UF-LeftCancellable.html#1941" class="Bound">f</a> <a id="1943" href="Sigma.html#436" class="Function">꞉</a> <a id="1945" class="Symbol">(</a><a id="1946" href="UF-LeftCancellable.html#1930" class="Bound">X</a> <a id="1948" class="Symbol">→</a> <a id="1950" href="UF-LeftCancellable.html#1934" class="Bound">Y</a><a id="1951" class="Symbol">)</a> <a id="1953" href="Sigma.html#436" class="Function">,</a> <a id="1955" href="GeneralNotation.html#1154" class="Function">left-cancellable</a> <a id="1972" href="UF-LeftCancellable.html#1941" class="Bound">f</a>

<a id="⌈_⌉"></a><a id="1975" href="UF-LeftCancellable.html#1975" class="Function Operator">⌈_⌉</a> <a id="1979" class="Symbol">:</a> <a id="1981" class="Symbol">{</a><a id="1982" href="UF-LeftCancellable.html#1982" class="Bound">X</a> <a id="1984" class="Symbol">:</a> <a id="1986" href="Universes.html#274" class="Generalizable">𝓤</a> <a id="1988" href="Universes.html#417" class="Function Operator">̇</a> <a id="1990" class="Symbol">}</a> <a id="1992" class="Symbol">{</a><a id="1993" href="UF-LeftCancellable.html#1993" class="Bound">Y</a> <a id="1995" class="Symbol">:</a> <a id="1997" href="Universes.html#276" class="Generalizable">𝓥</a> <a id="1999" href="Universes.html#417" class="Function Operator">̇</a> <a id="2001" class="Symbol">}</a> <a id="2003" class="Symbol">→</a> <a id="2005" href="UF-LeftCancellable.html#1982" class="Bound">X</a> <a id="2007" href="UF-LeftCancellable.html#1904" class="Function Operator">↣</a> <a id="2009" href="UF-LeftCancellable.html#1993" class="Bound">Y</a> <a id="2011" class="Symbol">→</a> <a id="2013" href="UF-LeftCancellable.html#1982" class="Bound">X</a> <a id="2015" class="Symbol">→</a> <a id="2017" href="UF-LeftCancellable.html#1993" class="Bound">Y</a>
<a id="2019" href="UF-LeftCancellable.html#1975" class="Function Operator">⌈</a> <a id="2021" href="UF-LeftCancellable.html#2021" class="Bound">f</a> <a id="2023" href="Sigma-Type.html#202" class="InductiveConstructor Operator">,</a> <a id="2025" class="Symbol">_</a> <a id="2027" href="UF-LeftCancellable.html#1975" class="Function Operator">⌉</a> <a id="2029" class="Symbol">=</a> <a id="2031" href="UF-LeftCancellable.html#2021" class="Bound">f</a>

<a id="2034" class="Keyword">infix</a> <a id="2040" class="Number">0</a> <a id="2042" href="UF-LeftCancellable.html#1904" class="Function Operator">_↣_</a>

<a id="2047" class="Markup">\end{code}</a><a id="2057" class="Background">
</a></pre></body></html>